plugins {
	id "se.bjurr.gradle.bundle-jar" version "1.6.0"
}

allprojects {
	javaExtension {
		javaVersion = 17
	}

	releaseExtension {
		nexusUsernameEnvOrProp = "OSSRH_USERNAME"
		nexusPasswordEnvOrProp = "OSSRH_TOKEN"
	}

	signExtension {
		signingKeyEnvOrProp = "OSSRH_GPG_SECRET_KEY"
		signingPasswordEnvOrProp = "OSSRH_GPG_SECRET_KEY_PASSWORD"
	}
}

project("wiremock-spring-boot", {
	dependencies {
		// Remove these when wiremock-jetty12:jar uses newer jetty
		// https://github.com/wiremock/wiremock/issues/3139
		implementation "org.eclipse.jetty:jetty-server:12.1.4"
		implementation "org.eclipse.jetty:jetty-proxy:12.1.4"
		implementation "org.eclipse.jetty:jetty-alpn-server:12.1.4"
		implementation "org.eclipse.jetty:jetty-alpn-java-server:12.1.4"
		implementation "org.eclipse.jetty:jetty-alpn-java-client:12.1.4"
		implementation "org.eclipse.jetty:jetty-alpn-client:12.1.4"
		implementation "org.eclipse.jetty.ee10:jetty-ee10-servlet:12.1.4"
		implementation "org.eclipse.jetty.ee10:jetty-ee10-servlets:12.1.4"
		implementation "org.eclipse.jetty.ee10:jetty-ee10-webapp:12.1.4"
		implementation "org.eclipse.jetty.http2:jetty-http2-server:12.1.5"

		api "org.wiremock:wiremock-jetty12:3.13.2"

		// Don't want to force these versions on the user
		[
			"org.springframework.boot:spring-boot-test:4.0.0-RC2",
			"org.springframework:spring-test:7.0.0",
			"org.slf4j:slf4j-api:2.0.17",
			"org.junit.jupiter:junit-jupiter-api:6.0.1"
		].each {
			compileOnly it
			testImplementation it
		}
	}
})

project("wiremock-spring-boot-standalone", {
	dependencies {
		api(project(":wiremock-spring-boot")) {
			exclude group: '*'
		}
		api "org.wiremock:wiremock-standalone:3.13.2"
	}
})

subprojects {
	dependencies {
		testImplementation "org.springframework.boot:spring-boot-starter-test:4.0.0-RC2"
		testImplementation "org.springframework.boot:spring-boot-starter-web:4.0.0-RC2"

		testImplementation "org.assertj:assertj-core:3.27.6"

		testImplementation platform("org.junit:junit-bom:6.0.1")
		testImplementation "org.junit.platform:junit-platform-suite:6.0.1"
		testImplementation 'org.junit.jupiter:junit-jupiter'
		testImplementation 'org.junit.platform:junit-platform-launcher'

		testImplementation "io.rest-assured:rest-assured:5.5.6"
		testImplementation "io.rest-assured:rest-assured:5.5.6"

		testImplementation "io.cucumber:cucumber-java:7.31.0"
		testImplementation "io.cucumber:cucumber-spring:7.31.0"
		testImplementation "io.cucumber:cucumber-junit-platform-engine:7.31.0"
	}
}